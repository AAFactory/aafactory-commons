apply from: '../dependencies.gradle'
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools
    
    defaultConfig {
        applicationId "io.github.aafactory.sample"
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion versions.compileSdk
        versionCode versions.publishVersionCode
        versionName versions.publishVersion
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
    
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"
    // If you need to add more flavors, consider using flavor dimensions.
    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
            dimension "default"
        }
        prod {
            dimension "default"
        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name == 'release'
                && variant.flavors[0].name == 'mock') {
            variant.ignore = true
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
    
    androidExtensions {
        experimental = true
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    configurations {
        all*.exclude group: 'com.android.support', module: 'support-v13'
    }
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Android Architecture Components
    implementation "android.arch.lifecycle:extensions:$versions.archLifecycleVersion"
    implementation "android.arch.persistence.room:runtime:$versions.roomVersion"
    kapt "android.arch.persistence.room:compiler:$versions.roomVersion"
    
    // android
    implementation "com.android.support:appcompat-v7:$versions.supportLib"
    implementation "com.android.support:cardview-v7:$versions.supportLib"
    implementation "com.android.support:design:$versions.supportLib"
    implementation "com.android.support:support-v4:$versions.supportLib"
    implementation 'com.android.support.constraint:constraint-layout:2.0.0-alpha2'
    implementation "com.android.support.test.espresso:espresso-idling-resource:$versions.espressoVersion"
    implementation "com.android.volley:volley:${VOLLEY_VERSION}"
    
    // google
    implementation "com.google.android.gms:play-services-location:$versions.gms"
    implementation "com.google.guava:guava:19.0"
    implementation "com.google.dagger:dagger:$versions.dagger"
    implementation "com.google.dagger:dagger-android:$versions.dagger"
    implementation "com.google.dagger:dagger-android-support:$versions.dagger"
    kapt "com.google.dagger:dagger-compiler:$versions.dagger"
    kapt "com.google.dagger:dagger-android-processor:$versions.dagger"
    
    // jetbrains
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.19.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:0.19.3'
    
    // square
    implementation "com.squareup.okhttp3:okhttp:$versions.okhttp3"
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit2"
    implementation "com.squareup.retrofit2:adapter-rxjava:$versions.retrofit2"
    implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit2"
    implementation "com.squareup.retrofit2:converter-gson:$versions.retrofit2"
    implementation 'com.squareup.picasso:picasso:2.5.2'
    
    // implementation project(':simple-commons')
    implementation 'com.simplemobiletools:commons:3.7.8'
    implementation project(':commons')
    
    implementation 'ru.gildor.coroutines:kotlin-coroutines-retrofit:0.8.2'   // Coroutines - Retrofit extention
    implementation 'com.github.medyo:fancybuttons:1.8.4'
    implementation 'com.tapadoo.android:alerter:2.0.5'
    implementation 'de.hdodenhof:circleimageview:1.3.0'
    implementation 'com.github.thomper:sweet-alert-dialog:v1.4.0'
    implementation 'com.simplecityapps:recyclerview-fastscroll:1.0.17'
    implementation 'com.xw.repo:bubbleseekbar:3.17'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    implementation 'com.github.PhilJay:MPAndroidChart-Realm:v2.0.2@aar'
    implementation (name: 'naver-map-api-2.1.3', ext: 'aar')
    implementation 'com.github.hotchemi:permissionsdispatcher:3.3.1'
    implementation 'com.github.bumptech.glide:glide:4.5.0'
    implementation 'com.afollestad.material-dialogs:core:2.0.0-alpha06'
    implementation 'com.afollestad.material-dialogs:input:2.0.0-alpha06'
    implementation 'com.zoonref:simple-view-behavior:1.0'
    implementation 'com.luffykou:android-common-utils:1.1.3'
    kapt 'com.github.bumptech.glide:compiler:4.5.0'
    kapt "com.github.hotchemi:permissionsdispatcher-processor:3.3.1"
    
    // reactivex
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'io.reactivex:rxjava:1.3.4'

    provided 'org.glassfish:javax.annotation:10.0-b28'

    // Dependencies for local unit tests
    testImplementation "junit:junit:$versions.junitVersion"
    testImplementation "org.hamcrest:hamcrest-all:$versions.hamcrestVersion"
    testImplementation "org.mockito:mockito-core:$versions.mockitoVersion"
    
    // Android Testing Support Library's runner and rules
    androidTestImplementation "com.android.support.test:runner:$versions.runnerVersion"
    androidTestImplementation "com.android.support.test:rules:$versions.runnerVersion"
    
    // Dependencies for Android unit tests
    androidTestImplementation "junit:junit:$versions.junitVersion"
    androidTestImplementation "org.mockito:mockito-android:$versions.mockitoVersion"
    
    // Espresso UI Testing
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$versions.espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$versions.espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$versions.espressoVersion"
    
    // Resolve conflicts between main and test APK:
    androidTestImplementation "com.android.support:support-annotations:$versions.supportLib"
    androidTestImplementation "com.android.support:support-v4:$versions.supportLib"
    androidTestImplementation "com.android.support:recyclerview-v7:$versions.supportLib"
    androidTestImplementation "com.android.support:appcompat-v7:$versions.supportLib"
    androidTestImplementation "com.android.support:design:$versions.supportLib"
    androidTestImplementation "com.android.support.test.espresso.idling:idling-concurrent:$versions.espressoVersion"
    androidTestImplementation "android.arch.persistence.room:testing:$versions.roomVersion"
}

repositories {
    mavenCentral()
    maven { url "http://repository.jetbrains.com/all" }
    maven { url "https://dl.bintray.com/drummer-aidan/maven/" }
}